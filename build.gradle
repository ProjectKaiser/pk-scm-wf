plugins {
	id 'java'
	id 'maven'
	id 'eclipse'
	id 'idea'
	id 'jacoco'
	id 'com.github.kt3k.coveralls' version '2.8.1'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

group = 'org.scm4j'
version = file('version').text.trim()

jacocoTestReport {
	reports {
		xml.enabled = true // coveralls plugin depends on xml format report
		html.enabled = true
	}
}

configurations.all {
	resolutionStrategy {
		cacheDynamicVersionsFor 0, 'seconds'
		cacheChangingModulesFor 0, 'seconds'
	}
}

repositories {
	maven { url "https://jitpack.io" }
	mavenCentral()
}

test {
	jvmArgs '-Xmx128m'
 	testLogging {
		events "failed"
		exceptionFormat "full"
	}
}

dependencies {
	// load managed dependencies
	def s = '[ \t]*', w = '[^:#@]+?'
	(file('mdeps').text =~ /(?m)^$s($w):($w):($w)?(:$w)?(@$w)?$s(?:#$s($w)?$s)?$/).each {
			match, group, name, version, classifier, ext, configuration ->
		add(configuration ?: name.contains('test') ? 'testCompile' : 'compile', "$group:$name:${version?:''}${classifier?:''}${ext?:''}", { changing = true })
	}

	testCompile 'com.github.stefanbirkner:system-rules:1.16.1'
	testCompile 'junit:junit:4.12'
	testCompile 'org.mockito:mockito-core:2.0.62-beta'
	testCompile 'nl.jqno.equalsverifier:equalsverifier:2.3'
	testCompile 'org.hamcrest:hamcrest-all:1.3'

	compile 'com.google.guava:guava:23.0'
	compile 'org.yaml:snakeyaml:1.18'
	compile 'org.slf4j:slf4j-simple:1.7.21'
	compile 'org.projectlombok:lombok:1.16.18'
}

jar {
	manifest.attributes (
			'Specification-Title': project.name,
			'Specification-Version': version,
			'Implementation-Title': project.name,
			'Implementation-Version': "$version (${new Date().format('yyyy-MM-dd')})",
	)
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives sourcesJar
	archives javadocJar
}